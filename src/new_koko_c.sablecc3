Package org.newkoko.c;


Helpers
    all	= [0 .. 127] ;
    digit = ['0' .. '9'] ;
    not_digit = ['_' + [['a' .. 'z'] + ['A' .. 'Z']]] ;
    digit_sequence = digit+ ;
    fractional_constant	= digit_sequence ? '.' digit_sequence | digit_sequence '.' ;
    sign = '+' | '-' ;
    exponent_part = ( 'e' | 'E' ) sign ? digit_sequence ;
    escape_sequence = '\' ''' | '\"' | '\?' | '\\' | '\a' | '\b' | '\f' | '\n' | '\r' | '\t' | '\v' ;
    string_char	= [ all - [ '"' + [ '\' + [ 10 + 13 ] ] ] ] | escape_sequence ;
    string_char_sequence = string_char+ ;
    not_zero_digit = ['1' .. '9'] ;
    cr = 13 ;
    lf = 10 ;
    eol = cr lf | cr | lf ;
    not_cr_lf = [all - [cr + lf]] ;
    not_star = [all - '*'] ;
    not_star_slash = [not_star - '/'] ;
    tab = 9;
    short_comment = '//' not_cr_lf* eol ;
    long_comment = '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/' ;


Tokens

    dot = '.' ;
    comma = ',' ;
    colon = ':' ;
    semicolon = ';' ;
    l_par = '(' ;
    r_par = ')' ;
    l_bracket = '[' ;
    r_bracket = ']' ;
    l_brace = '{' ;
    r_brace = '}' ;
    star = '*' ;
    div = '/' ;
    mod = '%' ;
    ampersand = '&' ;
    plus = '+' ;
    minus = '-' ;
    excl_mark = '!' ;
    quest_mark = '?' ;
    eq = '=' ;
    eq_eq = '==' ;
    not_eq = '!=' ;
    lt = '<' ;
    lt_eq = '<=' ;
    gt = '>' ;
    gt_eq = '>=' ;
    ampersand_ampersand = '&&' ;
    bar_bar = '||' ;
    star_equal = '*=' ;
    div_equal = '/=' ;
    mod_equal = '%=' ;
    plus_equal = '+=' ;
    minus_equal = '-=' ;
    case = 'case' ;
    if = 'if' ;
    else = 'else' ;
    switch = 'switch' ;
    while = 'while' ;
    for = 'for' ;
    in = 'in' ;
    continue = 'continue' ;
    break = 'break' ;
    return = 'return' ;
    fun = 'fun' ;
    void = 'void' ;
    int = 'int' ;
    long = 'long' ;
    double = 'double' ;
    identifier = not_digit (digit | not_digit)* ;
    floating_constant = fractional_constant exponent_part? | digit_sequence exponent_part ;
    string_literal = '"' string_char_sequence? '"' ;
    integer_constant = not_zero_digit digit* | '0';
    blank = (eol | tab | ' ')+ ;
    comment = short_comment | long_comment ;


Ignored Tokens
    blank,
    comment;


Productions
    program = function_or_statement* ;

    function_or_statement = {function} function
                            | {statement} statement ;

    function = function_declaration function_body ;

    function_declaration = fun identifier l_par parameter_list? r_par function_type ;

    function_type = {void} void
                    | type ;

    type = {int} int
            | {long} long
            | {double} double ;

    parameter_list = parameter next_parameter* ;

    parameter = type identifier ;

    next_parameter = comma parameter ;

    function_body = l_brace statement* stop_statement? r_brace ;

    statement = {simple_statement} simple_statement semicolon
                | {if} if conditional_expression statement_block
                | {if_else} if conditional_expression [if_block]:statement_block
                            else [else_block]:statement_block
                | {while} while conditional_expression statement_block
                | {for} for identifier in [from]:value colon [to]:value statement_block ; // TODO switch?

    conditional_expression = {relation} [left]:value relation_op [right]:value
                             | {value} value; // TODO multi conditional expression

    relation_op = {eq_eq} eq_eq
                | {not_eq} not_eq
                | {lt} lt
                | {lt_eq} lt_eq
                | {gt} gt
                | {gt_eq} gt_eq ;

    simple_statement = {new_variable} new_variable
                        | {assign_variable} assign_variable
                        | {call_expression} call_expression ;

    new_variable = type identifier eq right_hand_side ;

    assign_variable = identifier assign_op right_hand_side ;

    assign_op = {eq} eq
                | {plus_eq} plus_equal
                | {minus_eq} minus_equal
                | {star_eq} star_equal
                | {div_eq} div_equal
                | {mod_eq} mod_equal ;

    right_hand_side = {unary} unary_expression
                        | {binary} binary_expression ;

    binary_expression = {binary_expression} [l_value]:value binary_op [r_value]:value ;

    binary_op = {relation_op} relation_op
                | {star} star
                | {div} div
                | {mod} mod
                | {plus} plus
                | {minus} minus
                | {ampersand_ampersand} ampersand_ampersand
                | {bar_bar}	bar_bar ;

    unary_expression =  {value} value
                       | {call} call_expression
                       | {unary_op}	unary_op identifier
                       | {par_unary_op} l_par unary_op identifier r_par ;

    unary_op = {minus} minus
                | {excl_mark} excl_mark ;

    call_expression	= identifier l_par arg_list? r_par ;

    statement_block = l_brace statement* stop_statement? r_brace ;

    arg_list = value arg_list_tail* ;

    arg_list_tail = comma value ;

    stop_statement = {return} return semicolon
                        | {return_value} return value semicolon
                        | {return_par} return l_par value r_par semicolon ;

    value = {identifier} identifier
            | {constant} constant ;

    constant = {string} string_literal
                | {integer} minus? integer_constant
                | {double} minus? floating_constant ;